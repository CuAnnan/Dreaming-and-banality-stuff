@create fae_code=10
&CMD.FAEPOSE fae_code=$^\+(faepose|fpose|fp) (.+)$:@assert [u(can_see_fae, %#)]={@pemit %#=[cmdmsg(+faepose,Sorry\, you can't use this command.)]};@pemit/list/oneeval setr(players, u(can_see_fae_in_room))=%xh<Fae> %xn%n %2
@set fae_code/CMD.FAEPOSE = regexp
&CAN_SEE_FAE fae_code=[and(match(lcstr(rxlevel(%0)),chimera),match(lcstr(txlevel(%0)),chimera))]
&CMD.FAEEMIT fae_code=$^\+(fe|faeemit|femit) (.+)$:@assert [u(can_see_fae, %#)]={@pemit %#=[cmdmsg(+faepose,Sorry\, you can't use this command.)]};@pemit/list/oneeval setr(players, u(can_see_fae_in_room))=%xh<Fae>%xn %2
@set fae_code/CMD.FAEEMIT = regexp
&CMD.CANTRIP fae_code=$^\+cantrip(\/job \\d+)?(\/n|\/night|\/nightmare)? (\?\:(\?\:art\/|realm\/)?([a-zA-Z'\-]+))\s+\+\s+(\?\:(\?\:art\/|realm\/)?([a-zA-Z'\-]+))(\?\:\\s+([\\-\\+])\\s*(\\d+)\\s*)?(\?\:\\s+vs\\s+(\\d+))?$:th [setq(a, capstr(lcstr(index(setr(0,filterstats(%#, %3)),%b,2,1))))][setq(1,getstat3(%#,%q0))][setq(b, capstr(lcstr(index(setr(2,filterstats(%#, %4)),%b,2,1))))][setq(3,getstat3(%#,%q2))][setq(4, if(%6,%b%5%b%6))][setq(c,add(%q1, %q3, ifelse(%4,[[if(!comp(%5,-),-)]%6],0)))][setq(d,ifelse(%7,%7,6))])][setq(n, get(%#/cgtmp_stat_pool-Nightmare))][setq(e, max(0, sub(%qc, %qn)))][setq(f, min(%qc, %qn))];@assert [!and(t(%2), lt(%qe, 3))]=@pemit %#=You do not have enough regular dice to burn nightmare dice on this cantrip.;@if %2=[setq(e, sub(%qe,3))][setq(f, add(%qf,3))];@pemit/list/oneeval [setr(x, u(can_see_fae_in_room))]=[center(%xh%xc+cantrip%xn,76,=)]%r%xx%xh%n%xn cast a cantrip with [setr(m, %qa + %qb%q4)] vs %qd%rRoll: [if(gt(%qe, 0),setr(g, u(fnc.generateroll,%qe,%qd)))][if(%qf,%b%xh%xm[setr(h,u(fnc.generateroll,%qf,%qd))][setq(z,extract(u(fnc.sortroll,%qh,%d),5,1,|))]%xn)]%rResults: [setr(n,[extract([setr(r, u(fnc.sortroll,%qg %qh, %qd))], 2,1,|)] succeses.[if(extract(%qr,6,1,|),%b%xr%xhBotch!%xn)])]%r[repeat(=,76)];@dolist/inline %qx=&sys_rolllog %d0=setr(o,%xh+CANTRIP>%xn %n: %qm vs %qd ->%qg %qh. %qn)[if(hasattr(%d0,sys_rolllog),|[extract(get(%d0/sys_rolllog),1,19,|)])];@if %1={@sudo %#=+request/commentspecial [last(%1)]=%qo};@if %qz={&CGTMP_STAT_POOL-NIGHTMARE %#=[setr(v,if(lt(setr(y,add(u(%#/CGTMP_STAT_POOL-NIGHTMARE), %qz)), 10),%qy,mod(%qy,10)))]; @pemit %#=%xh%xmYou have gained %qz Nightmare, your new Nightmare score is %qv;&SYS_GAINLOG %#=nightmare %qz Nightmare Dice [secs()][if(hasattr(%#,sys_gainlog),|[extract(get(%#/sys_gainlog),1,19,|)])]}
@set fae_code/CMD.CANTRIP = regexp
&CAN_SEE_FAE_IN_ROOM fae_code=[iter(lcon(%L/CONNECT),if(and(visibleto(%#,##), u(can_see_fae, ##)),##,))]
&CMD.UNLEASH fae_code=$^\+unleash(\/job \\d+)? (\?\:(\?\:art\/|realm\/)?([a-zA-Z'\-]+))(\?\:\\s+([\\-\\+])\\s*(\\d+)\\s*)?(\?\:\\s+vs\\s+(\\d+))?$:&CGTMP_STAT_POOL-NIGHTMARE %#=[setr(n,ifelse(gte(setr(t,u(%#/CGTMP_STAT_POOL-NIGHTMARE)),9),[setq(i,1)]0,add(%qt,1)))];th [setq(a, capstr(lcstr(index(setr(0,filterstats(%#, %2)),%b,2,1))))][setq(l,getstat3(%#,%q0))][setq(g, getstat3(%#, pool glamour))][setq(0, u(fnc.generateroll,%qg,6))][setq(1, u(fnc.generateroll,%qn,6))][setq(r, u(fnc.sortroll,%q0 %q1,6))][setq(z,extract(%qr,5,1,|))];@pemit/list/oneeval [setr(x, u(can_see_fae_in_room))]=[center(%xh%xc+unleash%xn,76,=)]%r%xh%xx%n%xn unleashed the Art of %qa%rPool of [add(%qn, %qg)]%rRoll: [setr(p, %q0[if(%qn,%b%xh%xm%q1%xn)])]%rResults: [setr(n, extract(%qr, 2,1,|))] successes. [ifelse(extract(%qr,6,1,|),%xh%xr[lit(You botched. Please inform a Producer or Director who will mediate the result.)]%xn,ifelse(lte(%qn,0),%xh%xr[lit(You failed to Unleash. You cannot use that Art for the remainder of the scene.)]%xn,ifelse(lte(%qn,%ql),You manage to control the Unleashing.,The Unleashing succeeds but is beyond your control. Please inform a Producer or Director who will mediate the result.)))]%r[repeat(=,76)];@if %qz={&CGTMP_STAT_POOL-NIGHTMARE %#=[setr(v,if(lt(setr(y,add(u(%#/CGTMP_STAT_POOL-NIGHTMARE), %qz)), 10),%qy,mod(%qy,10)))]; @pemit %#=%xh%xmYou have gained %qz Nightmare, your new Nightmare score is %qv;&SYS_GAINLOG %#=nightmare %qz Nightmare Dice [secs()][if(hasattr(%#,sys_gainlog),|[extract(get(%#/sys_gainlog),1,19,|)])]};@dolist/inline %qx=&sys_rolllog %d0=setr(o,%xh+UNLEASHING>%xn %n: %qn vs 6 ->%qp. %qn successes;[if(lte(%qn,0), %Botch!)])[if(hasattr(%d0,sys_rolllog),|[extract(get(%d0/sys_rolllog),1,19,|)])];@if %1={@sudo %#=+request/commentspecial [last(%1)]=%qo};
@set fae_code/CMD.UNLEASH = regexp
&FNC.SORTROLL fae_code=%0|[setq(j,words(trim(iter(%0,if(gte(##,%1),##)))))][setq(k,words(trim(iter(%0,if(eq(##,1),##)))))][max(0,sub(%qj,%qk))]|%qj|%qk|[words(trim(iter(%0,if(eq(##,10),##))))]|[and(eq(0,%qj), gt(%q,0))]|%1
&FNC.READROLL fae_code=%0|[setq(b,words(trim(iter(%0,if(gte(##,%1),##)))))][setq(c,words(trim(iter(%0,if(eq(##,1),##)))))][max(0,sub(%qb,%qc))]|%qb|%qc|[words(trim(iter(%0,if(eq(##,10),##))))]|[and(eq(0,%qb), gt(%qc,0))]
&FNC.GENERATEROLL fae_code=sort(edit(extract(roll(%0,%1),4,3,|),|,%b))
@rxlevel fae_code=Real
@txlevel fae_code=Real Admin Chimera Wraith
@set fae_code=SAFE INHERIT !NO_COMMAND
